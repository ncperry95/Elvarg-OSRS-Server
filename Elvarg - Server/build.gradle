plugins {
  id 'application'
  id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories { mavenCentral() }

java {
  // Build with JDK 17 tooling (emits Java 8 bytecode below)
  toolchain { languageVersion = JavaLanguageVersion.of(21) }
}
tasks.withType(JavaCompile).configureEach {
  options.release = 8  // RSPS client/server friendliness
}

application {
  mainClass = 'com.elvarg.Elvarg'
}

sourceSets {
  main {
    java.srcDirs = ['src']
    resources.srcDirs = ['resources']
  }
}

dependencies {
  implementation 'io.netty:netty-all:4.1.109.Final'
  implementation 'com.google.guava:guava:32.1.3-jre'
  implementation 'com.google.code.gson:gson:2.10.1'
  implementation 'org.apache.commons:commons-compress:1.26.1'
  implementation 'org.slf4j:slf4j-simple:2.0.12'
}

tasks.named('jar') {
  manifest { attributes 'Main-Class': 'com.elvarg.Elvarg' }
}

tasks.named('shadowJar') {
  archiveBaseName.set('elvarg-server')
  archiveClassifier.set('all')
  archiveVersion.set('')
}



